plugins {
    id 'java-library'
    id 'io.spring.dependency-management'
    id 'org.openapi.generator'
}

group = "$projectGroup"
version = "$projectVersion"

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom("org.springframework.boot:spring-boot-dependencies:$springBootVersion")
    }
    dependencies {
        dependency("com.google.code.findbugs:jsr305:$jsr305Version")
        dependency("org.javamoney:moneta:$monetaVersion")
    }
}

dependencies {
    api(project(':libraries:base'))

    compileOnly('jakarta.servlet:jakarta.servlet-api')
    compileOnly('com.google.code.findbugs:jsr305')

    implementation('org.springframework:spring-context')
    implementation('org.springframework:spring-webmvc')
    implementation('org.springframework.data:spring-data-jdbc')
    implementation('com.fasterxml.jackson.core:jackson-annotations')
    implementation('org.javamoney:moneta')

    testImplementation('org.springframework:spring-test')
    testImplementation('org.junit.jupiter:junit-jupiter')
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$projectDir/catalog.0.0.oas.yaml"

    apiPackage = "coffeehouse.contracts.catalog.web"
    modelPackage = "coffeehouse.contracts.catalog.web.model"
    configOptions.set([
            "annotationLibrary"    : "none",
            "delegatePattern"      : "true",
            "documentationProvider": "none",
            "interfaceOnly"        : "true",
            "openApiNullable"      : "false",
            "useSpringBoot3"       : "true",
            "useSwaggerUI"         : "false",
    ])
}

sourceSets {
    main {
        java {
            srcDirs = ["$projectDir/src/main/java", "$buildDir/generate-resources/main/src/main/java"]
        }
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of("$javaVersion"))
    }
}

tasks.withType(JavaCompile).configureEach {
    dependsOn(tasks.openApiGenerate)
    options.compilerArgs.add("-parameters")
    options.encoding = "$javaOptionEncoding"
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}
